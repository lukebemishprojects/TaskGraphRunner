pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            url "https://maven.lukebemish.dev/releases/"
        }
    }
    plugins {
        id 'com.gradleup.shadow' version '+'
    }
}

buildscript {
    dependencyLocking {
        lockMode = LockMode.STRICT
        lockAllConfigurations()
        lockFile.set file("settings-gradle-buildscript.lockfile")
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '+'
    id 'dev.lukebemish.managedversioning' version '+'
    id 'dev.lukebemish.conventions' version '+'
}

gradle.lifecycle.beforeProject {
    it.buildscript{
        dependencyLocking {
            lockMode = LockMode.STRICT
            lockAllConfigurations()
        }
    }
}

managedVersioning {
    versionFile.set file('version.properties')
    versionPRs()
    versionSnapshots()

    publishing {
        mavenStaging()
        mavenSnapshot()
        mavenPullRequest()
        mavenCentral()
    }

    gitHubActions {
        snapshot {
            prettyName.set 'Snapshot'
            workflowDispatch.set(true)
            onBranches.add 'main'
            gradleJob {
                buildCache()
                cacheReadOnly.set false
                javaVersion.set '21'
                name.set 'build'
                gradlew 'Build', 'build'
                gradlew 'Publish', 'publish'
                mavenSnapshot('github')
            }
        }
        release {
            prettyName.set 'Release'
            workflowDispatch.set(true)
            gradleJob {
                buildCache()
                javaVersion.set '21'
                name.set 'build'
                readOnly.set false
                setupGitUser()
                gradlew 'Tag Release', 'tagRelease'
                gradlew 'Build', 'build'
                push()
                recordVersion 'Record Version', 'version'
                dependencySubmission()
            }
            gradleJob {
                buildCache()
                javaVersion.set '21'
                name.set 'publish'
                needs.add('build')
                tag.set('${{needs.build.outputs.version}}')
                gradlew 'Publish', 'publish', 'closeAndReleaseSonatypeStagingRepository'
                sign()
                mavenCentral()
                mavenStaging('github')
            }
        }
        build_pr {
            prettyName.set 'Build PR'
            pullRequest.set(true)
            gradleJob {
                javaVersion.set '21'
                name.set 'build'
                gradlew 'Build', 'build'
                gradlew 'Publish', 'publish'
                pullRequestArtifact()
            }
        }
        publish_pr {
            concurrency.unsetConvention()
            concurrency.unset()
            prettyName.set 'Publish PR'
            publishPullRequestAction(
                'github',
                "dev/lukebemish/taskgraphrunner",
                'Build PR'
            )
        }
    }
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        maven {
            name 'ParchmentMC'
            url 'https://maven.parchmentmc.org/'
            content {
                includeGroupAndSubgroups 'org.parchmentmc'
            }
        }
        maven {
            name 'Staging'
            url 'https://maven.lukebemish.dev/staging/'
        }
    }

    repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
}

rootProject.name = 'taskgraphrunner'

